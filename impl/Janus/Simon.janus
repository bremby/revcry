// https://eprint.iacr.org/2013/404.pdf

/* Rotate 'x' by 'r' bits to the left */
procedure rotate_left_u32(u32 x, int r)
    local int rm = r % 32
    local int rmc = 32 - rm
    local u32 y = (x << rm) | (x >> rmc)
    x <=> y
    delocal u32 y = (x >> rm) | (x << rmc)
    delocal int rmc = 32 - rm
    delocal int rm = r % 32

/* z0 = 4506230155203752166
 * z1 = 2575579794259089498
 * z2 = 3160415496042964403
 * z3 = 3957284701066611983
 * z4 = 3781244162168104175
 */

procedure set_T_j(int n, int m, int T, int j)
    if n = 16 then
        T += 32
    fi n = 16
    
    if n = 24 then
        T += 36
        if m = 4 then
            j += 1
        fi m = 4
    fi n = 24
    
    if n = 32 then
        if m = 3 then
            T += 42 
            j += 2
        fi m = 3
        if m = 4 then
            T += 44
            j += 3
        fi m = 4
    fi n = 32
    
    if n = 48 then
        if m = 2 then
            T += 52 
            j += 2
        fi m = 2
        if m = 3 then
            T += 54
            j += 3
        fi m = 3
    fi n = 48
    
    if n = 64 then
        if m = 2 then
            T += 68
            j += 2
        fi m = 2
        if m = 3 then
            T += 69
            j += 3
        fi m = 3
        if m = 4 then
            T += 72
            j += 4
        fi m = 4
    fi n = 64

procedure get_tmp(u32 k1, u32 k2, int m, u32 tmp, u32 garbage)
    tmp += k1
    uncall rotate_left_u32(tmp, 3)
    if m = 4 then
        tmp ^= k2
    fi m = 4
    garbage += tmp
    call rotate_left_u32(tmp, 1)
    tmp ^= garbage

procedure get_bit(int j, int i, u32 bit)
    local u32 z[5] = {0b11111010001001010110000111001101111101000100101011000011100110,
                      0b10001110111110010011000010110101000111011111001001100001011010,
                      0b10101111011100000011010010011000101000010001111110010110110011,
                      0b11011011101011000110010111100000010010001010011100110100001111,
                      0b11010001111001101011011000100000010111000011001010010011101111}
    if ((z[j] & (1 << (32 - i))) != 0) then
        bit += 1
    fi ((z[j] & (1 << (32 - i))) != 0)
    delocal u32 z[5] = {0b11111010001001010110000111001101111101000100101011000011100110,
                        0b10001110111110010011000010110101000111011111001001100001011010,
                        0b10101111011100000011010010011000101000010001111110010110110011,
                        0b11011011101011000110010111100000010010001010011100110100001111,
                        0b11010001111001101011011000100000010111000011001010010011101111}

procedure Simon_key_expansion(u32 key[], u32 key_expanded[])
    local int m = size(key)
    local int T = 0
    local int j = 0
    local u32 tmp = 0
    local u32 garbage = 0
    local u32 bit = 0
    call set_T_j(32, m, T, j)
    iterate int i = 0 by 1 to (m-1)
        key_expanded[i] += key[i]
    end
    iterate int i = m by 1 to (T-2)
        call get_tmp(key_expanded[i-1], key_expanded[i-3], m, tmp, garbage)
        call get_bit(j, i-m % 62, bit)
        //k[i] += ~k[i-m] ^ tmp ^ z[j][i-m % 62] ^ 3
        key_expanded[i] += ~key_expanded[i-m] ^ tmp ^ bit ^ 3
        uncall get_bit(j, i-m % 62, bit)
        uncall get_tmp(key_expanded[i-1], key_expanded[i-3], m, tmp, garbage)
    end
    uncall set_T_j(32, m, T, j)
    delocal u32 bit = 0
    delocal u32 garbage = 0
    delocal u32 tmp = 0
    delocal int j = 0
    delocal int T = 0
    delocal int m = size(key)

// we don't want to employ Benett's trick, I think that's counterproductive in our case (for cryptoanalysis)

procedure Simon_encipher(u32 data[2], u32 key[])
    local int T = 0
    local int j = 0
    call set_T_j(32, size(key), T, j)
    local u32 key_expanded[T]
	local int i = 0
    local u32 x1 = 0 //these variables are a nightmare to get rid of
    local u32 x2 = 0
    local u32 x8 = 0
    local u32 rx = 0
    
    call Simon_key_expansion(key, key_expanded)
    from i = 0 loop
        x1 += data[0]
        uncall rotate_left_u32(x1, 1)
        x2 += data[0]
        uncall rotate_left_u32(x2, 2)
        x8 += data[0]
        uncall rotate_left_u32(x8, 8)
        /* The AND operation is lossful, i.e. it cannot be reversed without knowing all of its original operands.
         * Therefore, both x1 and x8 cannot be cleared just by using the rest of the variables used on the line below.
         * I need to think about this more.
         *   - that would imply decryption is impossible. The trick is we don't overwrite x, we keep it for the next round.
         * 
         * From the inverse round function from the specs:
         * R(x, y)    = (y ^ f(x) ^ k, x)
         * R^-1(x, y) = (y, x ^ f(x) ^ k)
         * (x,y) = R^-1(R(x,y)) = R^-1(y ^ f(x) ^ k, x) = (x, y ^ f(x) ^ k ^ f(x) ^ k)) = (x,y)
         */
        // R(x, y) = (y ^ f(x) ^ k, x)
        data[1] ^= (x1 & x8) ^ x2 ^ key_expanded[i]
        data[0] <=> data[1]
        //x2 -= data[0] ^ (x1 & x8) ^ key_expanded[i]
        // R^-1(x, y) = (y, x ^ f(x) ^ k)
        rx += data[1]
        call rotate_left_u32(x1, 1)
        x1 -= rx
        call rotate_left_u32(x2, 2)
        x2 -= rx
        call rotate_left_u32(x8, 8)
        x8 -= rx
        rx -= data[1]
        i += 1
    until i = T
    uncall Simon_key_expansion(key, key_expanded)
    
    delocal u32 rx = 0
    delocal u32 x8 = 0
    delocal u32 x2 = 0
    delocal u32 x1 = 0
    delocal int i = T
    delocal u32 key_expanded[T]
    uncall set_T_j(32, size(key), T, j)
    delocal int j = 0
    delocal int T = 0


procedure main()
    u32 key[4]
    u32 data[2]

    key[0] += 0
    key[1] += 1
    key[2] += 2
    key[3] += 3
    
    data[0] += 42
    data[1] += 27
    
    
    
    show(data)
    call Simon_encipher(data, key)
    show(data)
    uncall Simon_encipher(data, key)
    show(data)
